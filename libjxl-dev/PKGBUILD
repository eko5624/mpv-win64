pkgname=libjxl-dev
pkgver=0.6.1
pkgrel=1
pkgdesc='JPEG XL image format reference implementation'
arch=('x86_64')
url="https://github.com/libjxl/libjxl/"
license=('BSD')
source=("git+https://github.com/libjxl/libjxl")
md5sums=('SKIP')

build() {
  # Fix AVX2 related crash due to unaligned stack memory
  export CXXFLAGS="$CXXFLAGS -Wa,-muse-unaligned-vector-move"
  export CFLAGS="$CFLAGS -Wa,-muse-unaligned-vector-move"
 
  cd $srcdir/libjxl
  git submodule update --init --recursive --depth 1 --recommend-shallow third_party/{brotli,highway,skcms}  
  rm -rf build && mkdir build && cd build
  $CMAKE .. -G "MinGW Makefiles" -DCMAKE_INSTALL_PREFIX=/opt -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_SHARED_LIBS=OFF \
    -DCMAKE_POSITION_INDEPENDENT_CODE=ON \
    -DJPEGXL_STATIC=OFF \
    -DBUILD_TESTING=OFF \
    -DJPEGXL_EMSCRIPTEN=OFF \
    -DJPEGXL_BUNDLE_LIBPNG=OFF \
    -DJPEGXL_ENABLE_TOOLS=OFF \
    -DJPEGXL_ENABLE_VIEWERS=OFF \
    -DJPEGXL_ENABLE_DOXYGEN=OFF \
    -DJPEGXL_ENABLE_EXAMPLES=OFF \
    -DJPEGXL_ENABLE_MANPAGES=OFF \
    -DJPEGXL_ENABLE_JNI=OFF \
    -DJPEGXL_ENABLE_PLUGINS=OFF \
    -DJPEGXL_ENABLE_DEVTOOLS=OFF \
    -DJPEGXL_ENABLE_BENCHMARK=OFF \
    -DJPEGXL_ENABLE_SJPEG=OFF  
  $CMAKE --build .
}

package() {
  cd $srcdir/libjxl/build
  $CMAKE --install . --prefix=$pkgdir/opt
  sed -i 's/Requires.private: libhwy libbrotlicommon libbrotlienc libbrotlidec/Requires.private: libhwy/' $pkgdir/opt/lib/pkgconfig/libjxl.pc
  sed -i 's/Libs.private: -lm/Libs.private: -lm -lstdc++/' $pkgdir/opt/lib/pkgconfig/libjxl.pc  
  sed -i 's/Libs.private: -lm/Libs.private: -lm -lstdc++/' $pkgdir/opt/lib/pkgconfig/libjxl_threads.pc 
}
