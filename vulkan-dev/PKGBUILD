# Maintainer: nyfair <nyfair2012@gmail.com>
pkgname=vulkan-shared-dev
pkgver=1.3.283.0
pkgrel=1
pkgdesc='Vulkan Header and Loader'
arch=('x86_64')
url="https://www.khronos.org/vulkan/"
license=('APACHE')
source=("git+https://github.com/KhronosGroup/Vulkan-Headers#tag=vulkan-sdk-$pkgver"
        "git+https://github.com/KhronosGroup/Vulkan-Loader#tag=vulkan-sdk-$pkgver")
md5sums=('SKIP' 'SKIP')

build() {
  export CFLAGS="${CFLAGS} -D__STDC_FORMAT_MACROS -Wno-unused-function"
  export CPPFLAGS="${CPPFLAGS} -D__STDC_FORMAT_MACROS"
  export CXXFLAGS="${CXXFLAGS} -D__USE_MINGW_ANSI_STDIO -D__STDC_FORMAT_MACROS -fpermissive -Wno-unused-function"

  cd $srcdir/Vulkan-Headers
  rm -rf build && mkdir build && cd build
  cmake .. \
    -G "Ninja" \
    -DCMAKE_INSTALL_PREFIX=/opt \
    -DCMAKE_BUILD_TYPE=Release
  cmake --install . --prefix=$srcdir/opt

  cd $srcdir/Vulkan-Loader
  # skip rc info and use vulkan-1.dll from your graphic driver
  sed -i '/loader.rc/d' loader/CMakeLists.txt
  sed -i '/RC_FILE_LOCATION/d' loader/CMakeLists.txt
  sed -i 's/-1.def/-1.def)/' loader/CMakeLists.txt
  rm -rf build && mkdir build && cd build
  cmake .. \
    -G "Ninja" \
    -DCMAKE_INSTALL_PREFIX=/opt \
    -DCMAKE_BUILD_TYPE=Release \
    -DVULKAN_HEADERS_INSTALL_DIR="${srcdir}/opt" \
    -DBUILD_TESTS=OFF \
    -DUSE_MASM=OFF \
    -DENABLE_WERROR=OFF
  cmake --build .
}

package() {
  cd $srcdir/Vulkan-Headers/build
  DESTDIR=$pkgdir cmake --install .
  cd $srcdir/Vulkan-Loader/build
  DESTDIR=$pkgdir cmake --install .
  mv $pkgdir/msys64/* $pkgdir
  rm -rf $pkgdir/msys64
  strip $pkgdir/opt/bin/vulkan*.dll
}
